## 1.2 Deriving Recursive Programs

> 推导递归程序


We have used the method of inductive definition to characterize complicated sets. We have seen that we can analyze an element of an inductively defined set to see how it is built from smaller elements of the set. We have used this idea to write a procedure in-S? to decide whether a natural number is in the set S. We now use the same idea to define more general procedures that compute on inductively defined sets.

> 我们使用了归纳定义的方法描述复杂集合。我们已经看到，利用此方法可以分析一个归纳定义集合的元素，看它是如何从集合的较小元素构建起来的。并且用这个思想编写了一个 `in-S?` 程序来判断一个自然数是否在集合 `S` 中。现在我们使用同样的思想去定义更一般的过程来计算归纳定义集。


Recursive procedures rely on an important principle:

> 递归程序依赖于一个重要原则:

```
-----------------------------------------------------------------------------------------------------
                        **The Smaller-Subproblem Principle**

If we can reduce a problem to a smaller subproblem, we can call the procedure that solves the problem to solve the subproblem.
-----------------------------------------------------------------------------------------------------
```

```
-----------------------------------------------------------------------------------------------------
                            **更小子问题原则**

如果我们可以将一个问题简化为一个较小的子问题，那么我们可以调用解决该问题的程序来解决该子问题。
-----------------------------------------------------------------------------------------------------
```


The solution returned for the subproblem may then be used to solve the original problem. This works because each time we call the procedure, it is called with a smaller problem, until eventually it is called with a problem that can be solved directly, without another call to itself.

> 子问题的求解方法可以被用来解决最初的问题。这是因为我们每次调用这个程序时，都会调用它自己去求解一个更小的问题，直到最终一个子问题可以被直接解决，而不需要再次调用它本身。


We illustrate this idea with a sequence of examples.

> 我们用一系列的例子来说明这个观点。


### 1.2.1 list-length

The standard Scheme procedure *length* determines the number of elements in a list.

> 标准Scheme函数`length` 用来确定列表中的元素个数。

```rkt
> (length '(a b c))
3

> (length '((x) ()))
2
```

Let us write our own procedure, called *list-length*, that does the same thing.

> 让我们编写自己的函数，叫做 `list-length` ，它同样用来求列表中元素的个数。


We begin by writing down the contract for the procedure. The contract specifies the sets of possible arguments and possible return values for the procedure. The contract also may include the intended usage or behavior of the procedure. This helps us keep track of our intentions both as we write and afterwards. In code, this would be a comment; we typeset it for readability.

> 首先写下函数的约束。该约束表明了函数可能参数集合以及可能的返回值。同样,包括了函数可能的预期用途或行为。这有助于我们在编写函数及之后跟踪我们的意图。在代码中，这将是一个注释; 这里仅仅为了可读性排版它。

```rkt
list-length : List -> Int
usage: (list-length l) = the length of l
(define list-length
    (lambda (lst)
        ...))
```

We can define the set of lists by

> 列表可以用BNF来定义

```
List ::= () | (Scheme_value . List)
```

Therefore we consider each possibility for a list. If the list is empty, then its length is 0.

> 因此，我们考虑一个列表的所有可能性，如果列表是空的，那么它的长度是0。

```rkt
list-length : List -> Int
usage: (list-length l) = the length of l
(define list-length
    (lambda (lst)
        (if (null? lst)
            0
            ...)))
```

If a list is non-empty, then its length is one more than the length of its cdr. This gives us a complete definition.

> 如果一个列表非空，那么它的长度比它的 `cdr` 的长度大 `1`。 这给了我们一个完整的定义。

```rkt
list-length : List -> Int
usage: (list-length l) = the length of l
(define list-length
    (lambda (lst)
        (if (null? lst)
            0
            (+ 1 (list-length (cdr lst))))))
```

We can watch *list-length* compute by using its definition.

> 我们可以通过`list-length` 的定义来观测它的计算方式。

```rkt
  (list-length '(a (b c) d))
= (+ 1 (list-length '((b c) d)))
= (+ 1 (+ 1 (list-length '(d))))
= (+ 1 (+ 1 (+ 1 (list-length '()))))
= (+ 1 (+ 1 (+ 1 0)))
= 3
```


### 1.2.2 nth-element

The standard Scheme procedure *list-ref* takes a list *lst* and a zero-based index *n* and returns element number *n* of *lst*.

> Scheme的标准函数 `list-ref` 接收一个列表 `lst` 和一个零起始的索引 `n`, 返回 `lst` 的第 `n` 个元素.

```rkt
> (list-ref '(a b c) 0)
'a
> (list-ref '(a b c) 1)
'b
```

Let us write our own procedure, called nth-element, that does the same thing.

> 让我们编写自己的函数 `nth-element`，它的功能和`list-ref` 相同。


Again we use the definition of List above.

> 我们依然使用之前的 `List` 的定义.


What should *(nth-element lst n)* return when *lst* is empty? In this case, 
*(nth-element lst n)* is asking for an element of an empty list, so we report an error.

> 当`lst` 为空时 `(nth-element lst n)` 应该返回什么呢？这种情况下，`(nth-element lst n)` 寻找一个空列表的元素，因此我们报告了一个错误。


What should *(nth-element lst n)* return when *lst* is non-empty? The answer depends on *n*. If *n = 0*, the answer is simply the car of *lst*.

> 当 lst 非空时 `(nth-element lst n)` 应该返回什么？答案取决于 `n`。如果 `n = 0`，则返回 `lst` 的 `car`.


What should *(nth-element lst n)* return when *lst* is non-empty and *n != 0*? In this case, the answer is the *(n − 1)-st* element of the cdr of *lst*. Since *n ∈ N* and *n != 0* , we know that *n − 1* must also be in N, so we can find the *(n − 1)-st* element by recursively calling *nth-element*.

> 当 `lst` 非空且为 `n != 0` 时 `(nth-element lst n)` 应该返回什么? 在这种情况下，答案是 `lst` 的 `cdr` 的 `(n-1)`个元素。因为 `n ∈ N` 和 `n != 0`，我们知道 `n-1` 一定也在 `N` 中，所以我们可以通过递归调用 `nth-element` 来找到第 `(n-1)`个元素。


This leads us to the definition

> 这就引出了我们的定义

```rkt
nth-element : List × Int -> SchemeVal
usage: (nth-element lst n) = the n-th element of lst
(define nth-element
    (lambda (lst n)
        (if (null? lst)
            (report-list-too-short n)
            (if (zero? n)
                (car lst)
                (nth-element (cdr lst) (- n 1))))))

(define report-list-too-short
    (lambda (n)
        (eopl:error 'nth-element
            "List too short by ~s elements.~%" (+ n 1))))
```

Here the notation **nth-element** : *List × Int -> SchemeVal* means that **nth-element** is a procedure that takes two arguments, a list and an integer, and returns a Scheme value. This is the same notation that is used in mathematics when we write *f : A × B -> C*.

> 这里，nth-element : List × Int-> schemaeval 表示 nth-element 是一个程序，它接受两个参数，一个列表，一个整数，并返回一个 Scheme 值。这和我们写 `f: a × b -> c` 时用的符号是一样的。


The procedure *report-list-too-short* reports an error condition by calling *eopl:error*. 
The procedure *eopl:error* aborts the computation. Its first argument is a symbol that allows the error message to identify the procedure that called *eopl:error*.
The second argument is a string that is then printed in the error message.
There must then be an additional argument for each instance of the character sequence *~s* in the string. The values of these arguments are printed in place of the corresponding *~s* when the string is printed. A *~%* is treated as a new line.
After the error message is printed, the computation is aborted.
This procedure *eopl:error* is not part of standard Scheme, but most implementations of Scheme provide such a facility. We use procedures named *report-* to report errors in a similar fashion throughout the book.

> 函数`report-list-too-short` 通过调用 `eopl:error` 报告一个错误条件。 
> 函数 `eopl:error` 终止了计算。它的第一个参数是一个符号，该符号允许错误消息标识调用 `eopl:error`。
> 第二个参数是一个字符串，之后把错误消息中打印出来。
> 字符串中的字符序列 `~s` 的每个实例都必须有一个附加参数。在打印字符串时，这些参数的值将代替相应的 `~s` 进行打印。一个 `~%` 被视为新的一行。
> 输出错误消息后，计算将终止。
> 函数 `eopl:error` 不是Scheme标准的一部分，但是 Scheme 的大多数实现都提供了这样的功能。本书中，我们使用名为 `report-` 的函数以类似的方式报告错误。


Watch how *nth-element* computes its answer:

> 看看 `nth-element` 函数 如何计算出它的结果:

```rkt
  (nth-element '(a b c d e) 3)
= (nth-element '(b c d e) 2)
= (nth-element '(c d e) 1)
= (nth-element '(d e) 0)
= d
```

Here *nth-element* recurs on shorter and shorter lists, and on smaller and smaller numbers.

> 这里 `nth-element` 在越来越短的列表上递归，在越来越小的数字上递归。


If error checking were omitted, we would have to rely on *car* and *cdr* to complain about being passed the empty list, but their error messages would be less helpful. For example, if we received an error message from *car*, we might have to look for uses of *car* throughout our program.

> 如果省略了错误检查，我们将不得不依靠 `car` 和 `cdr` 来给出错误: '传递的是空列表'，但这样的错误消息将没有什么帮助。例如，如果我们收到来自 `car` 的错误消息，我们可能必须在整个程序中寻找 `car` 的使用。


Exercise 1.6 [*] If we reversed the order of the tests in *nth-element*, what would go wrong?

> 练习1.6 [*] 如果我们颠倒 `nth-element` 中测试的顺序，会出现什么错误？


Exercise 1.7 [\* \*] The error message from *nth-element* is uninformative. Rewrite *nth-element* so that it produces a more informative error message, such as "*(a b c)* does not have 8 elements."

> 练习1.7 [* *] 来自 `nth-element` 的错误消息没有提供信息。重写 `nth-element` ，以便生成一个信息更丰富的错误消息，例如"`(a b c)` 没有8个元素"


### 1.2.3 remove-first

The procedure *remove-first* should take two arguments: a symbol, *s*, and a list of symbols, *los*. It should return a list with the same elements arranged in the same order as *los*, except that the first occurrence of the symbol *s* is removed. If there is no occurrence of *s* in *los*, then *los* is returned.

> 函数 `remove-first` 应该有两个参数: 一个符号 `s` 和一个符号列表 `los`。它应该返回一个与 `los` 排列顺序相同的元素的列表，只是符号 `s` 的第一个出现被删除了。如果在 `los` 中没有出现 `s` ，那么返回 `los` 。


```rkt
> (remove-first 'a '(a b c))
(b c)

> (remove-first 'b '(e f g))
(e f g)

> (remove-first 'a4 '(c1 a4 c1 a4))
(c1 c1 a4)

> (remove-first 'x '())
()
```

Before we start writing the definition of this procedure, we must complete the problem specification by defining the set *List-of-Symbol* of lists of symbols. Unlike the s-lists introduced in the last section, these lists of symbols do not contain sublists.

> 在开始编写这个函数的定义之前，我们必须通过符号列表`List-of-Symbol` 集合来完成问题规范。与上一节中介绍的 `s-lists` 不同，这些符号列表不包含子列表。


```
List-of-Symbol ::= () | (Symbol . List-of-Symbol)
```

A list of symbols is either the empty list or a list whose *car* is a symbol and whose *cdr* is a list of symbols.

> 符号列表可以是空列表，也可以是 `car`为符号、 `cdr` 为符号列表的列表。


If the list is empty, there are no occurrences of *s* to remove, so the answer is the empty list.

> 如果列表为空，则不会出现要删除的 `s`，因此函数返回也是空列表.

```rkt

```

